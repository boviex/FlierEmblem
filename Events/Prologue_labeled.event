
#include eastdlib.event

EventPointerTable(0x7, ThisChapter)

ThisChapter:
  POIN TurnBasedEvents
  POIN CharacterBasedEvents
  POIN LocationBasedEvents
  POIN MiscBasedEvents
  POIN Dunno Dunno Dunno
  POIN Tutorial
  POIN TrapData HMExtraTrapData
  POIN Units Units
  POIN $0 $0 $0 //these are for skirmish units (ally 1, ally 2, ally 3)
  POIN $0 $0 $0 //(enemy 1, enemy 2, enemy 3)
  POIN BeginningScene EndingScene

Units:
  UNIT Seth Paladin 0x0 Level(1, Ally, 0) [13,9] 0b 0x0 0x6 0x8B3BC4 [SteelSword,SilverLance,Vulnerary,0x0] NoAI
  UNIT Eirika EirikaLord 0x0 Level(1, Ally, 0) [8,5] 0b 0x0 0x1 0x8B3BF4 [Vulnerary,0x0,0x0,0x0] NoAI
  UNIT

TurnBasedEvents:
  TURN 0x0 Turn1Tut [1,0] 0x8
  TURN 0x0 Turn2Tut [2,0] 0x0
  TURN 0x0 Turn3Tut [3,0] 0x0
  // This TURN event tries to occur every turn as long as Event ID 0x8 is False
  // However the BeginningScene sets Event ID 0x8 to True so it will not happen until
  // the first turn after Event ID 0x8 is set to False.
  TURN 0x8 ONeillAttack [1,255] 0x8
  END_MAIN

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
END_MAIN

MiscBasedEvents:
  DefeatBoss(EndingScene)
  AFEV 0x7 OneLeft 0x0 //This runs the OneLeft check after every action
  CauseGameOverIfLordDies
  END_MAIN

Dunno:
END_MAIN

Tutorial:
  //No idea how this works tbh
  WORD 0x89EF2B0 0x89EF328 0x89EF388 0x89EF398
  WORD 0x89EF46C 0x89EF4EC 0x89EF50C 0x89EF53C
  WORD 0x89EF56C 0x89EF5A8 0x89EF6E0 0x89EF758
  WORD 0x89EF7B8 0x89EF7C8 0x89EF7FC
  END_MAIN

//UNIT data
  Enemies:
  UNIT ONeill Fighter 0x0 Level(4, Enemy, 0) [14,8] 0b 0x0 0x1 0x8B3BFC [IronAxe,0x0,0x0,0x0] [0x6,0x3,0x0,0x0]
  UNIT 0x82 Fighter 0x0 Level(1, Enemy, 0) [14,7] 0b 0x0 0x1 0x8B3C04 [IronAxe,0x0,0x0,0x0] [0x0,0xA,0x0,0x0]
  UNIT 0x80 Fighter 0x0 Level(2, Enemy, 0) [14,7] 0b 0x0 0x1 0x8B3C0C [IronAxe,0x0,0x0,0x0] [0x0,0x12,0x2,0x0]
  UNIT

  ThroneRoomUnits:
  //These are cutscene units so the character number is unimportant
  UNIT 0xC5 Peer 0x0 Level(1, Ally, 0) [13,3] 0b 0x0 0x0 0x0 [0x0] NoAI
  UNIT 0x1 EirikaLord 0x0 Level(1, Ally, 0) [14,4] 0b 0x0 0x0 0x0 [0x0] NoAI
  UNIT 0x2 Paladin 0x0 Level(1, Ally, 0) [15,4] 0b 0x0 0x0 0x0 [0x0] NoAI
  UNIT 0x5 General 0x0 Level(1, Ally, 0) [11,7] 0b 0x0 0x0 0x0 [0x0] NoAI
  UNIT 0x6 General 0x0 Level(1, Ally, 0) [15,7] 0b 0x0 0x0 0x0 [0x0] NoAI
  UNIT 0x3 Knight 0x0 Level(1, Ally, 0) [7,14] 0b 0x0 0x0 0x0 [0x0] NoAI
  UNIT 0xC0 Knight 0x0 Level(1, Ally, 0) [10,14] 0b 0x0 0x0 0x0 [0x0] NoAI
  UNIT 0x4 Cavalier 0x0 Level(1, Ally, 0) [11,4] 0b 0x0 0x0 0x0 [0x0] NoAI
  UNIT

  Messenger:
  UNIT 0xF Soldier 0x0 Level(1, Ally, 0) [9,15] 0b 0x0 0x3 0x8B3CA0 [0x0] NoAI
  UNIT

  GradoRoyals:
  UNIT Vigarde General 0x0 Level(1, Enemy, 0) [13,10] 0b 0x0 0x1 0x8B3CB8 [0x0] NoAI
  UNIT Lyon_Ch17 Necromancer 0x0 Level(1, Enemy, 0) [12,11] 0b 0x0 0x1 0x8B3CC0 [0x0] NoAI
  UNIT

  GradoShamans:
  UNIT 0x80 Shaman 0x0 Level(1, Enemy, 0) [5,1] 0b 0x0 0x1 0x8B3CC8 [0x0] NoAI
  UNIT 0x80 Shaman 0x0 Level(1, Enemy, 0) [5,2] 0b 0x0 0x1 0x8B3CD0 [0x0] NoAI
  UNIT 0x80 Druid 0x0 Level(1, Enemy, 1) [19,10] 0b 0x0 0x1 0x8B3CD8 [0x0] NoAI
  UNIT 0x80 Druid 0x0 Level(1, Enemy, 1) [20,10] 0b 0x0 0x1 0x8B3CE0 [0x0] NoAI
  UNIT

  GradoCavalry:
  UNIT 0x83 GreatKnight 0x0 Level(1, Enemy, 0) [10,10] 0b 0x0 0x1 0x8B3CE8 [0x0] NoAI
  UNIT 0x83 GreatKnight 0x0 Level(1, Enemy, 0) [16,10] 0b 0x0 0x1 0x8B3CF0 [0x0] NoAI
  UNIT 0x84 Paladin 0x0 Level(1, Enemy, 0) [10,10] 0b 0x0 0x1 0x8B3CF8 [0x0] NoAI
  UNIT 0x84 Paladin 0x0 Level(1, Enemy, 0) [16,10] 0b 0x0 0x1 0x8B3D00 [0x0] NoAI
  UNIT 0x85 MageKnight 0x0 Level(1, Enemy, 0) [10,10] 0b 0x0 0x1 0x8B3D08 [0x0] NoAI
  UNIT 0x85 MageKnight 0x0 Level(1, Enemy, 0) [16,10] 0b 0x0 0x1 0x8B3D10 [0x0] NoAI
  UNIT

  Escapees:
  UNIT Seth Paladin 0x0 Level(1, Ally, 0) [9,3] 0b 0x0 0x2 0x8B3F20 [SteelSword,0x0,0x0,0x0] NoAI
  UNIT Franz Cavalier 0x0 Level(1, Ally, 0) [9,3] 0b 0x0 0x2 0x8B3F30 [0x0] NoAI
  UNIT Eirika EirikaLord 0x0 Level(1, Ally, 0) [6,5] 0b 0x0 0x1 0x8B3F40 [0x0] NoAI
  UNIT

  ValterGroup:
  UNIT Valter_Prologue WyvernKnight 0x0 Level(5, Enemy, 0) [14,5] 0b 0x0 0x2 0x8B3F48 [SilverLance,0x0,0x0,0x0] NoAI
  UNIT 0x80 WyvernRider 0x0 Level(1, Enemy, 1) [14,4] 0b 0x0 0x1 0x8B3F58 [0x0] NoAI
  UNIT 0x80 WyvernRider 0x0 Level(1, Enemy, 1) [14,6] 0b 0x0 0x1 0x8B3F60 [0x0] NoAI
  UNIT

TrapData:
ENDTRAP
ALIGN 4

HMExtraTrapData:
ENDTRAP
ALIGN 4

BeginningScene:
  CALL RenaisThroneCutscene  //throne room and other stuff
  SVAL 0x2 0x89EF1BC
  CALL RunTutIfEasyMode
  CHECK_TUTORIAL
  BNE 0x0 0xC 0x0
  ASMC 0xCF461 //run if tutorial
  LABEL 0x0
  ENUT 0x8 //this line prevents Oneill from aggroing immediately (See TURN events)
  LOAD1 0x1 Units
  ENUN
  SVAL 0x1 0xD
  SET_HP Seth //seth's hp = 0xd = 13
  FlashCursor(Eirika,60)
  MUSI //quiet music
  Text(0x1D,0x90D) //Princess Eirika! This way!
  MUNO //normal music
  MOVE 0x18 Seth [4,4] //move Seth
  ENUN
  FlashCursor(Seth,60)
  Text(0x90E) // Once we cross that bridge...
  SVAL 0x2 0x89EF27C
  CALL RunTutIfEasyMode
  _WARP 0x0 Eirika [4,5] //warp?? Eirika just moves normally...
  ENUN
  CALL GiveRapier
  CALL ONeillSpawn
  NoFade
  ENDA

RenaisThroneCutscene:
  SVAL 0xB 0xA000E //camera pos
  LOMA 0x10 //load cutscene map
  LOAD2 0x1 ThroneRoomUnits //LOAD2 because the cutscene blue units shouldn't be added to the roster
  ENUN
  FADU 16
  MUSC 0x26
  BROWNBOXTEXT 0x664 [8,8]
  LOAD1 0x1 Messenger
  ENUN
  _0x2620 0xE //unknown?
  FlashCursor(0xF,60) //the messenger guy
  Text(0x903) //defenses breached
  MOVE 0x0 0xF [13,11]
  ENUN
  DISA 0xF //soldier leaves
  MOVE_1STEP 0x0 Eirika left
  ENUN
  FlashCursor(Eirika,60)
  Text(0x904) //father, no
  MOVEONTO 0x0 Seth Eirika //seth rescues
  ENUN
  DISA Eirika
  FlashCursor(Seth,60)
  Text(0x905) //run away
  MOVE 0x0 Seth [13,11]

  SVAL 0xD 0x0 //set up coordinates to move to
  SVAL 0x1 0x10C
  SAVETOQUEUE
  SVAL 0x1 0x0
  SAVETOQUEUE
  SVAL 0x1 0x2CC
  SAVETOQUEUE
  SVAL 0x1 0x0
  SAVETOQUEUE
  MOVEFORCED 0x0 Franz 0x0 //go to them in order
  ENUN
  DISA Seth
  DISA Franz //off screen you go
  MOVE 0x0 0x5 [11,4] //generals move in to protect the king
  MOVE 0x0 0x6 [15,4]
  ENUN
  MOVE_1STEP 0x0 0x5 0x1
  MOVE_1STEP 0x0 0x6 0x0
  ENUN
  LOAD1 0x1 GradoShamans //enemy units
  ENUN
  LOAD1 0x1 GradoCavalry
  ENUN
  LOAD1 0x1 GradoRoyals
  ENUN
  FlashCursor(0xC5,60) //Fado
  TEXTSTART
  TEXTSHOW 0x906 //eirika, ephraim... ded
  TEXTEND
  FADI 2
  REMA
  //clear out stuff in preparation for loading new map
  EVBIT_F 0x2
  CLEA 0x0
  CLEE 0x0
  CLEN 0x0
  SVAL 0xB 0x0 //camera to 0,0
  LOMA 0x40 //outside
  FADU 16
  LOAD2 0x1 Escapees //seth, franz. Still LOAD2 bc cutscene blue units
  ENUN
  FlashCursor(Seth,60)
  Text(0x1D,0x907) //Franz, ride ahead
  SVAL 0xD 0x0  //another move path
  SVAL 0x1 0x104
  SAVETOQUEUE
  SVAL 0x1 0x0
  SAVETOQUEUE
  SVAL 0x1 0x84
  SAVETOQUEUE
  SVAL 0x1 0x0
  SAVETOQUEUE
  SVAL 0x1 0x80
  SAVETOQUEUE
  SVAL 0x1 0x0
  SAVETOQUEUE
  MOVEFORCED 0x0 Franz 0x0
  ENUN
  DISA Franz
  FlashCursor(Seth,60)
  Text(0x1D, 0x908) //behind me
  LOAD1 0x1 ValterGroup //LOAD1 bc cutscene enemies can do that
  ENUN
  MOVE_1STEP 0x0 Seth Right
  ENUN
  MOVE_1STEP 0x0 Eirika Left
  ENUN
  FlashCursor(Valter_Prologue,60)
  Text(0x1D, 0x909) //gimme the girl
  MOVE_1STEP 0x0 Valter_Prologue Left
  ENUN
  StartBattle //scripted fight setup
  MissedAttack(0,0) //Seth misses
  NormalDamage(1,0) //Valter hits
  EndAttack
  FIG1 Seth Valter_Prologue 0x0 //execute fight
  FlashCursor(Seth,60)
  Text(0x90B) //ahh, no

  //seth 'rescues' eirika
  MOVE_1STEP 0x8 Seth left
  ENUN
  DISA Eirika

  SVAL 0xD 0x0 //movement path again
  SVAL 0x1 0x18104
  SAVETOQUEUE
  SVAL 0x1 0x0
  SAVETOQUEUE
  SVAL 0x1 0x18084
  SAVETOQUEUE
  SVAL 0x1 0x0
  SAVETOQUEUE
  SVAL 0x1 0x18080
  SAVETOQUEUE
  SVAL 0x1 0x0
  SAVETOQUEUE
  MOVEFORCED 0x0 Seth 0x0
  ENUN
  DISA Seth //run awayy
  FlashCursor(Valter_Prologue,60)
  Text(0x90C) //such terrible wounds etc
  FADI 16
  EVBIT_F 0x2
  CLEA 0x0 //clear out all the cutscene units
  CLEE 0x0
  CLEN 0x0
  SVAL 0xB 0x0 //set camera to 0,0
  LOMA 0x0 //load prologue map
  FADU 16
  ENDA

GiveRapier:
  FlashCursor(Seth,60)
  Text(0x90F)
  CALL RemoveBGIfNeeded //this is unknown, seems to remove the background?
  SVAL 0x3 Rapier
  GIVEITEMTO Eirika
  SVAL 0x2 0x89EF828
  CALL RunTutIfEasyMode
  ENDA

ONeillSpawn:
  LOAD1 0x1 Enemies
  ENUN
  FlashCursor(ONeill,60)
  MUSC 0x13
  Text(0x910) //Renais dogs etc
  ENUF 0x4
  ENDA

OneLeft:
  CHECK_ENEMIES //get the number of enemies and put it in Slot 0xC
  SVAL 0x7 0x1 //Put the number to compare against in Slot 0x7
  BNE 0x0 0xC 0x7 //If Slot 0xC does not equal Slot 0x7 (enemy count not 1), go to LABEL 0x0
    CUMO 0x2
    STAL 60
    CURE
    TEXTSTART
    TEXTSHOW 0x913 //all that's left is their leader
    TEXTEND
    REMA
    ENUF 0x8 //this unsets the event ID so the next turn Oneill will agro (see TURN events)
    GOTO 0x1 //go to LABEL 0x1
  LABEL 0x0
    CHECK_EVENTID //put event id in slot C
    SADD 0xC2 //move it from C to 2
    ENUF_SLOT2 //unset the event (so it can keep checking)
  LABEL 0x1
  NoFade
  ENDA

ONeillAttack:
MUSC 0x13
Text(0x914) //wretched lordling
CHECK_TUTORIAL
BNE 0x0 0xC 0x0 //if slot c is not 0 i.e. easy mode, skip the AI change
  SVAL 0x1 0x0
  CHAI ONeill
LABEL 0x0
NoFade
ENDA

EndingScene:
MUSC 0x31
SetBackground(0x1D)
TEXTSHOW 0x918 //Princess, are you injured?
TEXTEND
FADI 16
REMA
ENUT 0xE0 //tons of permanent event IDs???
ENUT 0xE1
ENUT 0xB7
ENUT 0xB4
ENUT 0xB5
ENUT 0xDC
ENUT 0xB9
ENUT 0xC2
ENUT 0xC3
ENUT 0xE7
ENUT 0xC9
MNC2 0x1 //go to ch1 without world map (only works on some chapters)
ENDA

//Tutorial Data
  Turn1Tut:
  SVAL 0x2 0x89EF0A4
  CALL RunTutIfEasyMode
  SVAL 0x2 0x89EF3B4
  CALL RunTutIfEasyMode
  NoFade
  ENDA

  Turn2Tut:
  SVAL 0x2 0x89EF3D8
  CALL RunTutIfEasyMode
  NoFade
  ENDA

  Turn3Tut:
  SVAL 0x2 0x89EF610
  CALL RunTutIfEasyMode
  SVAL 0x2 0x89EF670
  CALL RunTutIfEasyMode
  NoFade
  ENDA

RemoveBGIfNeeded: //located at $591f40
EVBIT_MODIFY 0x1
EVBIT_F 0x2
CHECK_EVBIT 0x8
BEQ 0x0 0xC 0x0
FADU 16
LABEL 0x0
EVBIT_MODIFY 0x0
ENDA

RunTutIfEasyMode:
CHECK_TUTORIAL
BEQ 0x0 0xC 0x0
TUTORIAL_CALL
LABEL 0x0
ENDA
